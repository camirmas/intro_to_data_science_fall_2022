### A Pluto.jl notebook ###
# v0.19.12

using Markdown
using InteractiveUtils

# â•”â•â•¡ 640a599e-3a27-11ed-069c-f14f3b6633e9
using CairoMakie, CSV, DataFrames, Interpolations, LinearAlgebra, Statistics, JLD2, Distributions, PlutoUI

# â•”â•â•¡ 78a8749f-c501-4e7b-bccf-3bdf4d55df92
TableOfContents()

# â•”â•â•¡ dbf497fa-f45b-4f75-96c0-3e0a73af0203
md"# hw1: Monte Carlo simulation to compute the area of Crater lake

the task in this hw assignment is to compute the area of Crater Lake using a Monte Carlo simulation. this problem resembles the classic one of estimating $\pi$ by Monte Carlo simulation, described [here](https://en.wikipedia.org/wiki/Monte_Carlo_method#Overview).

to represent the shape of Crater Lake, I provide a matrix `X`, whose column $i$ is the spatial coordinate of a point $\mathbf{x}_i\in\mathbb{R}^2$ that lies on the boundary of the lake. the spatial units are miles. we will employ this data tracing the lake boundary to create a representation of the shape/area of the lake.
"

# â•”â•â•¡ 2e87f1a8-b943-4317-99b1-8125e99355f2
md"
## read in, viz the raw data
ğŸœ download the `crater_lake_coords.jld2` file from Github [here](https://github.com/SimonEnsemble/intro_to_data_science_fall_2022/blob/main/hw/mc_lake/crater_lake_coords.jld2). use the `load` function from the `JLD2` package (see [here](https://juliaio.github.io/JLD2.jl/dev/#save-and-load-functions)) to read the data and assign the matrix stored in the `.jld2` file as a variable `X`.

!!! note
	> `JLD2` saves and loads Julia data structures in a format comprising a subset of HDF5. -[JLD2 docs](https://github.com/JuliaIO/JLD2.jl)

	> Hierarchical Data Format (HDF) is a set of file formats (HDF4, HDF5) designed to store and organize large amounts of data. -[Wikipedia](https://en.wikipedia.org/wiki/Hierarchical_Data_Format)

"

# â•”â•â•¡ 4f83121f-4b98-4854-adb4-7e11be25b482
X = load("./crater_lake_coords.jld2")["X"]

# â•”â•â•¡ 60e98df1-e21a-4c8d-ba54-50455b61379c
md"ğŸœ use `Makie` to draw a scatter plot of the points tracing the lake (contained in `X`). 
* make the points blue, like lake water
* include x- and y-labels (well, xâ‚ and xâ‚‚ here) with units of miles
* include a title
* pass `aspect=DataAspect()` to `Axis` to ensure proper scaling. see [here](https://docs.makie.org/v0.17.13/examples/blocks/axis/index.html#controlling_axis_aspect_ratios).
I wrote a function `viz_lake(X)` to do this, but you don't have to.

!!! hint
	use `Makie.jl` to make a `scatter` plot. see [here](https://docs.makie.org/v0.17.13/tutorials/basic-tutorial/#scatter_plot).
"

# â•”â•â•¡ 68ad49c5-6bd5-460c-b404-33e52292dd6d
function drawlake()
	f = Figure()
	ax = Axis(f[1, 1],
	    title = "Crater Lake",
		titlesize = 24,
	    xlabel = "Miles",
	    ylabel = "Miles",
		aspect=DataAspect()
	)
	
	return f
end

# â•”â•â•¡ e9dbfd3f-a0d9-4dec-899b-aa62be5790dd
function drawlake(data)
	f = drawlake()
	scatter!(data[begin, :], data[end, :], label="Lake Coords")
	
	return f
end

# â•”â•â•¡ 010f8cc2-dbc9-4fa0-bc0a-2c088bfc00d5
function drawlake(data, interp_data)
	f = drawlake(data)
	x, y = interp_data
	lines!(x, y)

	return f
end

# â•”â•â•¡ fd1a78dc-2fb4-48f9-a61c-fa00ecfd367b
drawlake(X)

# â•”â•â•¡ 4ec36505-66d9-4cd0-a4c5-e3718d1a5cba
md"
## centering the data
it will be convenient to center the coordinates.

ğŸœ compute the \"center\" of the set of points tracing the lake as the mean coordinate in the matrix `X`. assign it to be a variable `xÌ„`.

i.e.
```math
	\bar{\mathbf{x}} = \frac{1}{N}\sum_{i=1}^N \mathbf{x}_i
```
with $\mathbf{x}_i\in\mathbb{R}^2$ one of $N$ coordinates on the lake boundary.

!!! hint
	use the `mean` function and the `dims` keyword argument. see [docs](https://docs.julialang.org/en/v1/stdlib/Statistics/#Statistics.mean).
"

# â•”â•â•¡ b45e7b0a-8644-4210-bec9-8fd3076b08f4
xÌ„ = mean(X; dims=2)

# â•”â•â•¡ 3a088002-b238-46eb-86f8-483ca5b9a7c2
md"ğŸœ define a centered coordinate matrix `XÌ‚` whose column $i$ gives the centered coordinate:
```math
	\mathbf{\hat{x}}_i := \mathbf{x}_i - \bar{\mathbf{x}}_i.
```
i.e., subtract the mean vector from each column of the matrix `X` to obtain `XÌ‚`. this is a simple shift that preserves distances.
"

# â•”â•â•¡ bc825fb1-5ee5-4d39-9ec2-a99ca8b68e79
XÌ‚ = X .- xÌ„

# â•”â•â•¡ 89dde6a1-1230-4841-8fca-c2563a54fd4a
md"ğŸœ redraw the lake boundary (exactly as above) using the centered coordinates. the lake should be the exact same shape, but centered at the origin $(0, 0)$."

# â•”â•â•¡ a9cf8d6e-1607-44e6-8f74-769ce7bff060
drawlake(XÌ‚)

# â•”â•â•¡ 9a6aa8a8-fd6a-4928-bf28-b7444b2e6d3b
md"## parameterizing the lake as $r=r(\theta)$

we need a continuous representation of the shape of the lake boundary.

to achieve this, let us parameterize the lake boundary as a function $r=r(\theta)$ with $\theta \in [-\pi, \pi]$ the angle a point on the lake boundary makes with the xâ‚-axis and $r$ the distance of that point from the origin. i.e., we will represent the lake boundary using a parameterization in polar coordinates. 
"

# â•”â•â•¡ e5079f42-2137-459f-a351-ddc22864085a
md"### translate the data into polar coordinates
first, let's translate the data, currently in Cartesian coordinates, into polar coordinates. 

ğŸœ create a vector `r` whose entry $i$ gives the distance of coordinate $\mathbf{x}_i$ from the origin $(0,0)$.
"

# â•”â•â•¡ 0edc2e54-6552-4a56-aa03-87ae8886e826
begin
	xdim, ydim = size(XÌ‚)
	r = zeros(ydim)
	for i=1:ydim
		x, y = XÌ‚[:, i] # grab column
		r[i] = âˆš(x^2 + y^2)
	end
	r
end

# â•”â•â•¡ d866aace-c019-4fe3-a77e-d681e1af001f
md"ğŸœ create a vector Î¸ whose entry $i$ gives the angle coordinate $\mathbf{x}_i$ makes with the xâ‚-axis.

!!! hint
	use the `atan` function [here](https://docs.julialang.org/en/v1/base/math/#Base.atan-Tuple{Number}).
"

# â•”â•â•¡ 6d01e662-761f-4f33-89d9-7d29c4196366
begin
	Î¸ = zeros(ydim)
	for i=1:ydim
		x, y = XÌ‚[:, i] # grab column
		Î¸[i] = atan(y, x)
	end
	Î¸
end

# â•”â•â•¡ cbce6c42-b5db-4b59-8399-495289c3b247
md"ğŸœ to check your representation of the data in polar cooradinates, plot the points tracing the lake boundary again but compute the $(x_1, x_2)$ Cartesian coordinates for `scatter` using the formulas $x_1=r \cos(\theta)$ and $x_2=r\sin(\theta)$ and your vectors `r` and `Î¸`.

!!! note
	this process of checking your code every step of the way is a good coding practice.
"

# â•”â•â•¡ d3a6961d-3f70-4b9e-8bc9-38e8ad91c952
begin
	checkdata_x = r .* cos.(Î¸)
	checkdata_y = r .* sin.(Î¸)
	checkdata = hcat(checkdata_x, checkdata_y)
	println(checkdata' .- XÌ‚) # should be zeros
	drawlake(checkdata') # plot
end

# â•”â•â•¡ d80c35e7-ee90-464e-b7b4-6e2250efc3ff
md"### creating a continuous representation $r=r(\theta)$
the goal now is to use linear interpolation of the data $(\theta_i, r_i)$ to construct a continuous function $r(\theta)$ for $\theta \in [-\pi, \pi]$ that traces the lake boundary. we will employ the `linear_interpolation` function [here](https://juliamath.github.io/Interpolations.jl/stable/#Example-Usage) from `Interpolations.jl`.
"

# â•”â•â•¡ c11fa2f9-a440-4b0e-85ba-27c749f4fccb
md"ğŸœ the `linear_interpolation` function requires the $\theta_i$ in the `Î¸` vector to be sorted (note, they currently are not). sort the values in `Î¸`. of course, the values in `r` must be permuted the same way, to preserve the pairing between the entries in `r` and `Î¸`. assign the sorted versions as variables `r_sorted` and `Î¸_sorted`.

!!! hint
	use the `sortperm` function, then slice both `Î¸` and `r`. see [here](https://docs.julialang.org/en/v1/base/sort/#Base.sortperm).
"

# â•”â•â•¡ b4addc63-8f97-4c15-8e86-e0f5733071ef
p = sortperm(Î¸)

# â•”â•â•¡ 95d4cfa4-964d-45f0-8496-6a7beb382072
Î¸_sorted = Î¸[p]

# â•”â•â•¡ 6fe5a3de-ddda-426a-a072-1db6892f22aa
r_sorted = r[p]

# â•”â•â•¡ d4a35029-ff51-41f4-ab80-e9e61455d0c1
md"the function $r(\theta)$ is periodic because $r(-\pi)=r(\pi)$. the `linear_interpolation` function can represent a periodic function. however, it requires us to repeat either the first or last data point by copying it onto the other side of the periodic boundary.

ğŸœ with $N$ the index of the last data point in the sorted arrays, prepend the array `Î¸_sorted` with $\theta_N-2\pi$ and the array `r_sorted` with $r_N$.
"

# â•”â•â•¡ d9849427-3389-4040-900b-d4eadc922c7d
N = length(Î¸)

# â•”â•â•¡ 4211a63e-c894-45fc-bb38-b13850dcae4d
prepend!(Î¸_sorted, [Î¸_sorted[N] - 2Ï€])

# â•”â•â•¡ ef735579-310e-4c2d-91f0-f7ac99d71cf0
prepend!(r_sorted, [r_sorted[N]])

# â•”â•â•¡ 7aa3e93e-d7f7-41b9-aaeb-0a6f13eca4f5
md"ğŸœ use the `linear_interpolation` function to construct the function $r(\theta)$ (the linear interpolator of the data $\{(\theta_i, r_i)\}$) and assign it as a variable `r_of_Î¸`. pass the keyword argument `extrapolation_bc=Periodic()` to the function `linear_interpolation` for the interpolator to implement periodic boundary conditions.
"

# â•”â•â•¡ 4d7ed267-7483-4fbb-9e4c-75b9474bb651
r_of_Î¸ = linear_interpolation(Î¸_sorted, r_sorted; extrapolation_bc=Periodic())

# â•”â•â•¡ 74e6ca9d-cb85-43d2-885c-2ab5ddaabe6f
md"ğŸœ to show the interpolator respects periodic boundary conditions, compute $r(\phi)$ and $r(\phi + 2 \pi)$ for some random $\phi$. they should match."

# â•”â•â•¡ 3f2858b0-daec-4e61-9d8d-1ca7bc704428
begin
	Ï• = Ï€/4
	r_of_Î¸(Ï•) == r_of_Î¸(Ï• + 2Ï€)
end

# â•”â•â•¡ b51e69d6-e80e-40b5-b835-59665b6cc9c5
md"ğŸœ to ensure your function $r(\theta)$ representing the continuous boundary of the lake makes sense, create a `range` of 500 $\theta$'s between $-\pi$ and $\pi$, compute the corresponding $r$'s, and plot the continuous-looking curve representing the boundary of the lake. also plot the (centered) data points, to ensure the parametric curve $r(\theta)$ is passing through the data points.
"

# â•”â•â•¡ 54f19024-3808-40c0-91a2-ad36d6a99f9e
begin
	Î¸_values = range(-Ï€, Ï€, 500)
	r_values = r_of_Î¸.(Î¸_values)
	x = [r_of_Î¸(Î¸)*cos(Î¸) for Î¸ in Î¸_values]
	y = [r_of_Î¸(Î¸)*sin(Î¸) for Î¸ in Î¸_values]
	
	drawlake(XÌ‚, (x, y))
end

# â•”â•â•¡ 78bd4975-8098-44d6-91ec-977540a11644
md"## Monte Carlo
now that we have a continuous representation of the lake boundary via $r=r(\theta)$, we can run a Monte Carlo simulation to estimate the area of the lake.

ğŸœ write a function `inside_lake(x, r_of_Î¸)` that takes as arguments:
* a centered coordinate `x` (a vector)
* your interpolation-based representation of the lake boundary `r_of_Î¸`
and returns
* `true` if `x` falls inside the lake boundary
* `false` if `x` falls outside the lake boundary

!!! hint
	inside the function, compute the polar coordinates of `x`. compare with the output of `r_of_Î¸` giving the lake boundary.
"

# â•”â•â•¡ 643006b0-d36e-4c7d-8179-5e52fa3366e1
function inside_lake((x, y), r_of_Î¸)
	r = âˆš(x^2 + y^2)
	Î¸ = atan(y, x)

	r_lake = r_of_Î¸(Î¸)

	return r <= r_lake
end

# â•”â•â•¡ aa4fa8e2-f34f-43ab-859d-21322c15e6d2
md"ğŸœ test your function.
* `inside_lake([0.0, 0.0], r_of_Î¸)` should return `true`.
* `inside_lake([-2, 2.0], r_of_Î¸)` should return `false`.
"

# â•”â•â•¡ 05b591a7-b70b-473c-9ad1-706e5bf95c0c
inside_lake([0.0, 0.0], r_of_Î¸)

# â•”â•â•¡ 24ce43ec-89ae-4175-9837-c0ddffdee98f
inside_lake([-2, 2.0], r_of_Î¸)

# â•”â•â•¡ 1ed3b8a8-34c4-44e5-876d-cecc4806a122
md"ğŸœ (conceptually) draw a square $[-L/2,L/2]^2$ around the lake boundary, with $L$ sufficiently large to include all of the lake.
assign the variable `L` as the maximum absolute value of a (centered) coordinate of the lake, to ensure the square encompasses the lake.
"

# â•”â•â•¡ 6d65a99e-455b-4fa0-be1a-0ef77e368913
L = round(maximum(r_sorted) * 2; digits=2) # max r * 2

# â•”â•â•¡ 59e6635c-f01f-4fc3-bb5a-873e2a88ff38
md"
think of a large, homogeneous raincloud covering the square of area $L^2$. the raincloud drops rain drops at uniform random locations in the square. we will simulate this by dropping raindrops at uniform random locations in the square. these raindrops can fall inside the lake or outside of it. the area of the lake is then $L^2$ times the fraction of the raindrops that fall inside the lake. 

ğŸœ write a function `run_monte_carlo(N, L, r_of_Î¸)` whose arguments are:
* `N`: the number of Monte Carlo samples (rain drops)
* `L`: the length of the square
* `r_of_Î¸`: the interpolation-based representation of $r(\theta)$ giving the lake boundary
and returns
* `X_rain`: a `2 Ã— N` matrix whose columns give the locations where the raindrops fell
* `rain_inside`: a `N`-length boolean matrix indicating whether the raindrops fell inside or outside the lake.

!!! hint
	inside the function, you need a way to generate a uniform random number in $[-L/2, L/2]$. I suggest you use `Uniform` from the `Distributions.jl` package [here](https://juliastats.org/Distributions.jl/latest/univariate/#Distributions.Uniform). you could use `rand()` as well, with some extra steps.
"

# â•”â•â•¡ 15d303b5-61c5-450e-8301-d7d2f0e822eb
function run_monte_carlo(N, L, r_of_Î¸)
	distr = Uniform(-L/2, L/2)
	X_rain = rand(distr, (2, N))
	rain_inside = [inside_lake(X_rain[:, i], r_of_Î¸) for i=1:N]

	return X_rain, rain_inside
end

# â•”â•â•¡ de00d71a-52c0-4879-9b2d-fc9f8b5e3f94
md"ğŸœ run your Monte carlo simulation with `N=10000`.
compute your estimate for the area of the lake. it should be close to what Wikipedia reports, 20.6 miÂ² (not clear if this includes Wizard Island or not).

!!! note 
	if your area estimate does not match that of Wikipedia within 1 miÂ², there must be a bug. the next step will help you diagnose.
"

# â•”â•â•¡ c1c5e37e-6a18-438e-ab56-376ecfecf3e7
function calc_area(N, L, r_of_Î¸)
	X_rain, rain_inside = run_monte_carlo(N, L, r_of_Î¸)
	n_inside = sum(rain_inside)
	area = L^2 * n_inside/N
	df = DataFrame(
		"x" => X_rain[1, :],
		"y" => X_rain[2, :],
		"rain_inside" => rain_inside
	)
	area, df
end

# â•”â•â•¡ a7659496-6694-4134-952c-152e3f098ba5
area, df = calc_area(10_000, L, r_of_Î¸)

# â•”â•â•¡ bab8f895-0913-49fb-ab6b-e49f0ab9af6a
area

# â•”â•â•¡ 8781b9bc-25e3-471f-a478-877223186f4b
md"ğŸœ draw a plot containing:
* the centered coordinates of the lake (as above)
* the continuous interpolation-based representation of $r(\theta)$ as a curve (as above)
and, in addition, to visualize the result of your Monte Carlo simulation:
* each rain drop that fell in the square, with raindrops that fell in the lake colored blue and raindrops that fell outside the lake colored brown. if your code is correct, you should see the blue lake surrounded by brown. use the `:+` marker for the raindrops.
"

# â•”â•â•¡ 12717627-4edc-445f-84df-5c3cc4c99a1c
outside_df, inside_df = groupby(df, "rain_inside")

# â•”â•â•¡ 9a261341-e380-41d3-8845-cc5ee5608248
begin
	f = drawlake(XÌ‚, (x, y))
	scatter!(outside_df[:, "x"], outside_df[:, "y"]; color = :brown, marker = :+)
	scatter!(inside_df[:, "x"], inside_df[:, "y"]; color = :blue, marker = :+)
	f
end

# â•”â•â•¡ 27301229-b35e-4fb0-8bf9-c6fe43434821
md"
## written questions
* run the simulation a few times and qualitatively note the variance in the estimated area of the lake. now set `N=100` and do the same. explain the difference in variance between the estimates. what do you conclude?
* check out the map of Crater Lake. it includes Wizard Island. how would you go about accounting for the area of water only, excluding Wizard Island, as a next step?
* would this strategy to parameterize the lake as $r=r(\theta)$ work with all lakes? for example, look up the shape of Lake Huron. how would you write an `inside_lake` function that works for Lake Huron? (I'm not sure how I would approach this, myself...)
"

# â•”â•â•¡ 7627b4d4-4663-468e-b5af-41d1ca53f96f
area2, _ = calc_area(100, L, r_of_Î¸)

# â•”â•â•¡ 6576b16d-472c-436c-8bc8-abe74aab46f0
area2

# â•”â•â•¡ 3faec184-f8e2-4778-807d-9230d99db339
md"""
At a glance, the runs with 10,000 samples appear to have significantly less variance, around .6 square miles. The runs with 100 samples have a much higher variance, around 3 square miles. This leads to a conclusion that a higher sample size reduces variance.

Wizard Island's area can be estimated using the same methods as above, namely by parameterizing a function that can determine whether a particular point is within the island's boundary. We could then run the Monte Carlo simulation to determine which points fall within the lake boundary and which points fall within the Island boundary. We could then determine the area as before, but subtracting the points that fall within the Island boundary from the lake's area.

This strategy would break down with a lake that is highly non-convex (highly variable border) such that there would be multiple $r$ values for any given $\theta$. One potential solution would be to partition the lake into manageable regions and then aggregate the area results.
"""

# â•”â•â•¡ Cell order:
# â• â•640a599e-3a27-11ed-069c-f14f3b6633e9
# â• â•78a8749f-c501-4e7b-bccf-3bdf4d55df92
# â•Ÿâ”€dbf497fa-f45b-4f75-96c0-3e0a73af0203
# â•Ÿâ”€2e87f1a8-b943-4317-99b1-8125e99355f2
# â• â•4f83121f-4b98-4854-adb4-7e11be25b482
# â•Ÿâ”€60e98df1-e21a-4c8d-ba54-50455b61379c
# â• â•68ad49c5-6bd5-460c-b404-33e52292dd6d
# â• â•e9dbfd3f-a0d9-4dec-899b-aa62be5790dd
# â• â•010f8cc2-dbc9-4fa0-bc0a-2c088bfc00d5
# â• â•fd1a78dc-2fb4-48f9-a61c-fa00ecfd367b
# â•Ÿâ”€4ec36505-66d9-4cd0-a4c5-e3718d1a5cba
# â• â•b45e7b0a-8644-4210-bec9-8fd3076b08f4
# â•Ÿâ”€3a088002-b238-46eb-86f8-483ca5b9a7c2
# â• â•bc825fb1-5ee5-4d39-9ec2-a99ca8b68e79
# â•Ÿâ”€89dde6a1-1230-4841-8fca-c2563a54fd4a
# â• â•a9cf8d6e-1607-44e6-8f74-769ce7bff060
# â•Ÿâ”€9a6aa8a8-fd6a-4928-bf28-b7444b2e6d3b
# â•Ÿâ”€e5079f42-2137-459f-a351-ddc22864085a
# â• â•0edc2e54-6552-4a56-aa03-87ae8886e826
# â•Ÿâ”€d866aace-c019-4fe3-a77e-d681e1af001f
# â• â•6d01e662-761f-4f33-89d9-7d29c4196366
# â•Ÿâ”€cbce6c42-b5db-4b59-8399-495289c3b247
# â• â•d3a6961d-3f70-4b9e-8bc9-38e8ad91c952
# â•Ÿâ”€d80c35e7-ee90-464e-b7b4-6e2250efc3ff
# â•Ÿâ”€c11fa2f9-a440-4b0e-85ba-27c749f4fccb
# â• â•b4addc63-8f97-4c15-8e86-e0f5733071ef
# â• â•95d4cfa4-964d-45f0-8496-6a7beb382072
# â• â•6fe5a3de-ddda-426a-a072-1db6892f22aa
# â•Ÿâ”€d4a35029-ff51-41f4-ab80-e9e61455d0c1
# â• â•d9849427-3389-4040-900b-d4eadc922c7d
# â• â•4211a63e-c894-45fc-bb38-b13850dcae4d
# â• â•ef735579-310e-4c2d-91f0-f7ac99d71cf0
# â•Ÿâ”€7aa3e93e-d7f7-41b9-aaeb-0a6f13eca4f5
# â• â•4d7ed267-7483-4fbb-9e4c-75b9474bb651
# â•Ÿâ”€74e6ca9d-cb85-43d2-885c-2ab5ddaabe6f
# â• â•3f2858b0-daec-4e61-9d8d-1ca7bc704428
# â•Ÿâ”€b51e69d6-e80e-40b5-b835-59665b6cc9c5
# â• â•54f19024-3808-40c0-91a2-ad36d6a99f9e
# â•Ÿâ”€78bd4975-8098-44d6-91ec-977540a11644
# â• â•643006b0-d36e-4c7d-8179-5e52fa3366e1
# â•Ÿâ”€aa4fa8e2-f34f-43ab-859d-21322c15e6d2
# â• â•05b591a7-b70b-473c-9ad1-706e5bf95c0c
# â• â•24ce43ec-89ae-4175-9837-c0ddffdee98f
# â•Ÿâ”€1ed3b8a8-34c4-44e5-876d-cecc4806a122
# â• â•6d65a99e-455b-4fa0-be1a-0ef77e368913
# â•Ÿâ”€59e6635c-f01f-4fc3-bb5a-873e2a88ff38
# â• â•15d303b5-61c5-450e-8301-d7d2f0e822eb
# â•Ÿâ”€de00d71a-52c0-4879-9b2d-fc9f8b5e3f94
# â• â•c1c5e37e-6a18-438e-ab56-376ecfecf3e7
# â• â•a7659496-6694-4134-952c-152e3f098ba5
# â• â•bab8f895-0913-49fb-ab6b-e49f0ab9af6a
# â•Ÿâ”€8781b9bc-25e3-471f-a478-877223186f4b
# â• â•12717627-4edc-445f-84df-5c3cc4c99a1c
# â• â•9a261341-e380-41d3-8845-cc5ee5608248
# â•Ÿâ”€27301229-b35e-4fb0-8bf9-c6fe43434821
# â• â•7627b4d4-4663-468e-b5af-41d1ca53f96f
# â• â•6576b16d-472c-436c-8bc8-abe74aab46f0
# â•Ÿâ”€3faec184-f8e2-4778-807d-9230d99db339
